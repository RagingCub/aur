# Maintainer: Loup Labelle <louplabele@hotmail.com>

pkgname=st
pkgver=0.8.4
pkgrel=1
pkgdesc='simple terminal implementation for X.'
arch=('i686' 'x86_64')
depends=('libxft')
makedepends=('ncurses' 'desktop-file-utils')
url="http://st.suckless.org"
license=("custom")
_patches=("https://st.suckless.org/patches/desktopentry/st-desktopentry-0.8.4.diff"
	"https://st.suckless.org/patches/anysize/st-anysize-0.8.4.diff")
source=("http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz"
	"config.h" "${_patches[@]}")
md5sums=('e00b074c0e5d55513745c99f027b7a34'
         'b791ca1280a65c9578a3c67181f1b6a1'
         '10ea6b655da6615a48641ca4fbb4203c'
         'db025b249ee0b63ea6831f09a7d26655')

prepare() {
	cd $srcdir/$pkgname-$pkgver
	# skip terminfo which conflicts with ncurses
	sed -i '/tic /d' Makefile
	cp $srcdir/config.h config.h
	for patch in "${_patches[@]}"; do
		echo "Applying patch $(basename $patch)..."
		patch -Np1 -i "$srcdir/$(basename $patch)"
	done
}

build() {
	cd $srcdir/$pkgname-$pkgver
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
